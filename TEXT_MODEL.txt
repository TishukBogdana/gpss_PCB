PROKAT STORAGE 2;
H_M STORAGE 2;
T_PROK TABLE MP$A,0.1,0.1,20; ТРАНЗИСТНОЕ ВРЕМЯ ЧЕРЕЗ ПРОКАТНЫЕ УСТАНОВКИ
T_SMD TABLE MP$A,0.1,0.1,20; ТРАНЗИТНОЕ ВРЕМЯ ЧЕРЕЗ АВТОМАТ МОНТАЖА
T_KAR TABLE MP$A,0.1,0.1,20; ТРАНЗИТНОЕ ВРЕМЯ ЧЕРЕЗ КАРУСЕЛЬ
T_HAND_MT TABLE MP$A,0.1,0.1,20; ТРАНЗИТНОЕ ВРЕМЯ ЧЕРЕЗ УЗЕЛ РУЧНОГО МОНТАЖА
T_CTRL TABLE MP$A,0.1,0.1,20; ТРАЗНИТНОЕ ВРЕМЯ ЧЕРЕЗ УЗЕЛ ОПТИЧЕСКОГО КОНТРОЛЯ
T_CTRL TABLE MP$A,0.1,0.1,20; ТРАЗНИТНОЕ ВРЕМЯ ЧЕРЕЗ УЗЕЛ ОПТИЧЕСКОГО КОНТРОЛЯ
T_TERM TABLE V$VAR,0.5,0.5,20; TERMINATE

SAVEVALUE B_B,0;
VAR VARIABLE AC1-X$B_B;

qT_PROK QTABLE PROK,0.1,0.1,20; 
qT_SMD QTABLE QSMD,0.1,0.1,20; 
qT_KAR QTABLE QKAR,0.1,0.1,20;
qT_HAND_MT QTABLE QHM,0.1,0.1,20; 
qT_CTRL QTABLE QCONT,0.1,0.1,20; 
*____________________________________________________________________________
	GENERATE (Uniform(91,0.6,1.4));  ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ
; ВХОД НА ПРОКАТНЫЕ УСТАНОВКИ
	MARK  A;
	QUEUE PROK;
	ENTER PROKAT;
	DEPART PROK;
	ADVANCE (Exponential(91,0,0.1));  ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ
	LEAVE PROKAT;
          TABULATE T_PROK;

;ВХОД НА АВТОМАТ SMD МОНТАЖА
ASMD	MARK A;
	QUEUE QSMD;
	SEIZE SMD;
	DEPART QSMD;
	ADVANCE (Uniform(91,0.3,0.7)); ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ
	RELEASE SMD;
	TABULATE T_SMD; 
	TRANSFER 0.8,,OVEN; ЛИБО НА КАРУСЕЛЬ ЛИБО В ПЕЧЬ

; ВХОД НА КАРУСЕЛЬ
KARUSEL 	MARK A;
  	QUEUE QKAR; 
	SEIZE KAR;
	DEPART QKAR;
	ADVANCE (Exponential(91,0,0.25)); ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ
	RELEASE KAR;
	TABULATE T_KAR;


*НА КОНВЕКЦИОННУЮ ПЕЧ
* ПЕЧ ИМЕЕТ 4 ТЕМПЕРАТУРНЫЕ ЗОНЫ, КАЖДАЯ ЗОНА ВЫДЕЛЕНА ОТДЕЛЬНЫМ ПРИБОРОМ,
* ЧТОБЫ ОТРАЗИТЬ РЕАЛЬНУЮ СИТАУЦИЮ, ТАК КАК В ПЕЧИ МОЖЕТ БЫТЬ НЕСКОЛЬКО ПЛАТ В РАЗНЫХ ЗОНАХ
* ВРЕМЯ НАХОЖДЕНИЯ В КАЖДОЙ ЗОНЕ ОДИНАКОВО
OVEN      MARK A 
;ЗОНА НАГРЕВА
	QUEUE QZONE_HEATING;
	SEIZE Z_H;
	DEPART QZONE_HEATING;
	ADVANCE 0.02;
	RELEASE Z_H;

;ЗОНА СТАБИЛИЗАЦИИ
	SEIZE Z_ST;
	ADVANCE 0.02;
	RELEASE Z_ST;

;ЗОНА ПЛАВЛЕНИЯ
	SEIZE Z_PL;
	ADVANCE 0.02;
	RELEASE Z_PL;

;ЗОНА ОХЛАЖДЕНИЯ
	SEIZE Z_COOL;
	ADVANCE 0.02;
	RELEASE Z_COOL;
	TRANSFER 0.01,,LOST1;
	TRANSFER 0.5,,ASMD;  
HAND_MOUNT MARK A;
	QUEUE QHM;
	ENTER H_M;
	DEPART QHM;
	ADVANCE (Exponential(91,0,0.25)+Exponential(91,0,0.25));   ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ
	LEAVE H_M;
	TABULATE T_HAND_MT;
	TRANSFER 0.05,,LOST2;    
            

CONTROL   MARK A;
	QUEUE QCONT;
	SEIZE CTRL;
	DEPART QCONT;
	ADVANCE (Exponential(91,0,0.25));  ДАЛЕЕ ЗАКОНЫ БУДУТ ВАРЬИРОВАТЬСЯ

	RELEASE CTRL;
	TABULATE T_CTRL;
	TRANSFER 0.05,,HAND_MOUNT;

TABULATE T_TERM;
SAVEVALUE B_B,AC1;
	TERMINATE 1;
LOST1 TERMINATE 1;
LOST2 TERMINATE 1;
START 100000;
